class CameraChangeHandler : CustomFloatCVar
{
  override double ModifyValue(Name CVarName, double val)
  {
    return clamp(val, 1.0, 89.0);
  }
}

class PickupFlagHandler : CustomBoolCVar
{
  override bool ModifyValue(Name CVarName, bool val)
  {
	  players[consoleplayer].mo.bPickup = val;
	  return val;
  }
}

class MKeyEvents : EventHandler 
{
  cvar isodists[MAXPLAYERS], isopitches[MAXPLAYERS], isoyaws[MAXPLAYERS];
  cvar isoaims[MAXPLAYERS], rotlefts[MAXPLAYERS], rotrights[MAXPLAYERS], isosel[MAXPLAYERS];
  cvar selx[MAXPLAYERS], sely[MAXPLAYERS], selz[MAXPLAYERS];
  cvar lookingdown[MAXPLAYERS];
  array <Actor> selectables;
  array <Line> switches;
  int selnum, selobjsize, selswsize;
  bool inobjarray, inswarray, cursec_usable;
  Vector3 delta;
  double target_angle, target_pitch, viewh;
  FLineTraceData t;
  Actor pmo;

  override void OnRegister()
  {
	  selectables.Clear(); selobjsize = 0;
	  switches.Clear(); selswsize = 0;
	  selnum = 0;
	  inobjarray = true;
	  inswarray = false;
	  cursec_usable = false;
	  for (int i = 0; i < MAXPLAYERS; i++)
	  {
		  isosel[i] = CVar.GetCVar("IsoSelected", players[i]);
		  isodists[i] = CVar.GetCVar("IsometricDist", players[i]);
		  isopitches[i] = CVar.GetCVar("IsometricPitch", players[i]);
		  isoyaws[i] = CVar.GetCVar("IsometricYaw", players[i]);
		  isoaims[i] = CVar.GetCVar("IsoAimAssist", players[i]);
		  rotlefts[i] = CVar.GetCVar("RotatingLeft", players[i]);
		  rotrights[i] = CVar.GetCVar("RotatingRight", players[i]);
		  selx[i] = CVar.GetCVar("SelXPos", players[i]);
		  sely[i] = CVar.GetCVar("SelYPos", players[i]);
		  selz[i] = CVar.GetCVar("SelZPos", players[i]);
		  lookingdown[i] = CVar.GetCVar("IsoLookingDown", players[i]);
	  }
  }
  
  //get angle and delta from two positions
  static vector3, double, double lookAt(Vector3 pos1, Vector3 pos2)
  {
	  //calculate difference between pos1 and pos2 (level.Vec3Diff takes portals into account)
	  
	  Vector3 delta=level.Vec3Diff(pos1, pos2);
		
	  //calculate angle and pitch to other actor
	  double target_angle = atan2(delta.y, delta.x);
	  double target_pitch = -asin(delta.z/delta.length());

	  return delta, target_angle, target_pitch;
  }

  //get shorted xy-distance between actor and line
  static double shortest_dxy(Actor mo, Line ln)
  {
	  double lnlength = (ln.v1.p - ln.v2.p).length();
	  if (lnlength == 0.0) return (mo.pos.xy - ln.v1.p).length();
	  else
	  {
		  return abs((ln.v2.p.y - ln.v1.p.y)* mo.pos.x - (ln.v2.p.x - ln.v1.p.x)* mo.pos.y
					 + ln.v2.p.x * ln.v1.p.y - ln.v2.p.y * ln.v1.p.x) / lnlength;
	  }
  }

  override bool InputProcess (InputEvent e)
  {
	  if (e.Type == InputEvent.Type_KeyDown)
		  SendNetworkEvent("MYKEYDOWN", e.KeyScan);

	  if (e.Type == InputEvent.Type_KeyUp)
		  SendNetworkEvent("MYKEYUP", e.KeyScan);
    
	  return false;
  }

  override void NetworkProcess(ConsoleEvent e)
  {			
    if (e.Name == "MYKEYDOWN")
    {
		pmo = players[e.Player].mo;
		int key1, key2;
		[key1, key2] = Bindings.GetKeysForCommand("rotate_cam_left");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
			rotlefts[e.Player].SetBool(true);
		else
		{
			[key1, key2] = Bindings.GetKeysForCommand("rotate_cam_right");
			if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
				rotrights[e.Player].SetBool(true);
		}

		[key1, key2] = Bindings.GetKeysForCommand("iso_zoom_in");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
		{
			if(players[e.Player].camera)
				SpectatorCamera(players[e.Player].camera).Init(isodists[e.Player].GetFloat(),
															   isoyaws[e.Player].GetFloat(),
															   isopitches[e.Player].GetFloat(), -1);
		}
		else
		{
			[key1, key2] = Bindings.GetKeysForCommand("iso_zoom_out");
			if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
			{
				if(players[e.Player].camera)
					SpectatorCamera(players[e.Player].camera).Init(isodists[e.Player].GetFloat(),
																   isoyaws[e.Player].GetFloat(),
																   isopitches[e.Player].GetFloat(), -1);
			}
		}
		[key1, key2] = Bindings.GetKeysForCommand("iso_aim_assist");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
		{
			isoaims[e.Player].SetBool(!isoaims[e.Player].GetBool());
			if (isoaims[e.Player].GetBool()) console.printf("Aim Assist Enabled");
			else console.printf("Aim Assist Disabled");
		}

		[key1, key2] = Bindings.GetKeysForCommand("clear_select");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
		{
			isosel[e.Player].SetBool(false);
		}

		[key1, key2] = Bindings.GetKeysForCommand("look_down");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
		{
			lookingdown[e.Player].SetBool(true);
		}

		[key1, key2] = Bindings.GetKeysForCommand("use_selected");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
		{
			if (isosel[e.Player].GetBool())
			{
				if (inswarray && (switches.Size() > selnum) && switches[selnum])
				{
					Vector3 midln = ((switches[selnum].v1.p.x + switches[selnum].v2.p.x)*0.5,
									 (switches[selnum].v1.p.y + switches[selnum].v2.p.y)*0.5,
									 pmo.pos.Z + viewh);
					[delta, target_angle, target_pitch] = lookAt(pmo.pos + (0, 0, viewh), midln);
					bool tbool = pmo.LineTrace(target_angle,
											   (pmo.pos.xy - midln.xy).length() + 8.0,
											   0, TRF_THRUACTORS | TRF_BLOCKUSE, viewh, data:t);
					bool swvisible = true;
					if (!tbool || (t.hitType != TRACE_HitWall) || !t.hitLine || (t.hitLine.Index() != switches[selnum].Index()))
					{
						// Center of wall not in line of sight of the player
						swvisible = !tbool;
						if (!tbool)
						{
							// Maybe the line isn't a wall! Check if it can for SPAC_UseBack condition
							int plside = Level.ActorOnLineSide(pmo, switches[selnum]);
							if (plside == 1 && !(switches[selnum].activation & SPAC_UseBack)) { swvisible = false; }
							if (plside == 0 && ((switches[selnum].activation & (SPAC_Use | SPAC_UseThrough | SPAC_UseBack)) == SPAC_UseBack))
							{ swvisible = false; }
						}
					}
					if (swvisible)
					{
						if (switches[selnum].activation & (SPAC_Use | SPAC_UseThrough | SPAC_UseBack))
						{
							int side = 0; // Level.ActorOnLineSide(pmo, switches[selnum]); // SPAC_UseBack gives me segfaults
							if (!switches[selnum].Activate(pmo, side, SPAC_Use)) // (side == 0 ? SPAC_Use : SPAC_UseBack)))
								pmo.A_StartSound("noselect", CHAN_ITEM);
						}
					} else {
						pmo.A_StartSound("noselect", CHAN_ITEM);
					}
				} else if (inobjarray && (selectables.Size() > selnum) && selectables[selnum]) {
					[delta, target_angle, target_pitch] = lookAt(pmo.pos + (0, 0, viewh),
																 selectables[selnum].pos + (0, 0, selectables[selnum].height*0.5));
					bool tbool = pmo.LineTrace(target_angle, players[e.Player].mo.UseRange, target_pitch,
											   TRF_ALLACTORS | TRF_BLOCKUSE, viewh, data:t);
					if (!tbool || (t.hitType != TRACE_HitActor) || !t.hitActor || (t.hitActor != selectables[selnum]))
					{
						pmo.A_StartSound("noselect", CHAN_ITEM);
					} else {
						if (selectables[selnum].bSPECIAL)
						{
							selectables[selnum].Touch(pmo);
						} else if (selectables[selnum].bUSESPECIAL && selectables[selnum].health > 0) {
							// This whole segment is only for old DECORATE type activate functions
							let thing = selectables[selnum];
							if (thing.activationtype & (THINGSPEC_Activate | THINGSPEC_Deactivate | THINGSPEC_Switch))
							{
								if ((thing.activationtype & THINGSPEC_Switch)
									&& !(thing.activationtype & (THINGSPEC_Activate | THINGSPEC_Deactivate)))
								{
									thing.activationtype |= THINGSPEC_Activate;
								}
								// Can it be activated?
								if (thing.activationtype & THINGSPEC_Activate)
								{
									thing.activationtype &= ~THINGSPEC_Activate; // Clear flag
									if (thing.activationtype & THINGSPEC_Switch) // Set other flag if switching
										thing.activationtype |= THINGSPEC_Deactivate;
									thing.Activate(pmo);
								}
								// If not, can it be deactivated?
								else if (thing.activationtype & THINGSPEC_Deactivate)
								{
									thing.activationtype &= ~THINGSPEC_Deactivate; // Clear flag
									if (thing.activationtype & THINGSPEC_Switch)	// Set other flag if switching
										thing.activationtype |= THINGSPEC_Activate;
									thing.Deactivate(pmo);
								}
							}
						} else if (!selectables[selnum].Used(pmo)) {
							pmo.A_StartSound("noselect", CHAN_ITEM);
						}
					}
				} else if (cursec_usable && !inswarray && !inobjarray) {
					if (!pmo.CurSector.SecActTarget || !pmo.CurSector.TriggerSectorActions(pmo, SectorAction.SECSPAC_Use))
						pmo.A_StartSound("noselect", CHAN_ITEM);
				}
				isosel[e.Player].SetBool(false);
			}
		}

		[key1, key2] = Bindings.GetKeysForCommand("cycle_select");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
		{
			viewh = players[e.Player].mo.height*0.5; // viewheight*players[e.Player].crouchfactor;
			if (isosel[e.Player].GetBool())
			{
				// Handle next selection change, if possible
				if (inswarray && (switches.Size() > 0))
				{
					if (++selnum >= switches.Size())
					{
						selnum = 0;
						if (!cursec_usable && (selectables.Size() > 0)) { inobjarray = true; inswarray = false; }
						else if (cursec_usable) { inswarray = false; }
					}
				} else if (inobjarray && (selectables.Size() > 0)) {					
					if (++selnum >= selectables.Size())
					{
						selnum = 0;
						if (!cursec_usable && (switches.Size() > 0)) { inobjarray = false; inswarray = true; }
						else if (cursec_usable) { inobjarray = false; }
					}
				} else if (cursec_usable && !inswarray && !inobjarray) {
					selnum = 0;
					if (switches.Size() > 0) inswarray = true;
					else if (selectables.Size() > 0) inobjarray = true;
				}

				// Update selected marker position
				if (cursec_usable && !inswarray && !inobjarray)
				{
					selx[e.Player].SetFloat(pmo.pos.X);
					sely[e.Player].SetFloat(pmo.pos.Y);
					selz[e.Player].SetFloat(pmo.floorz);
				} else if (inswarray && (switches.Size() > selnum)) {
					Vector3 midln = ((switches[selnum].v1.p.x + switches[selnum].v2.p.x)*0.5,
									 (switches[selnum].v1.p.y + switches[selnum].v2.p.y)*0.5,
									 pmo.pos.Z + viewh);
					[delta, target_angle, target_pitch] = lookAt(pmo.pos + (0, 0, viewh), midln);
					bool tbool = pmo.LineTrace(target_angle,
											   (pmo.pos.xy - midln.xy).length() + 8.0,
											   0, TRF_THRUACTORS | TRF_BLOCKUSE, viewh, data:t);
					if (!tbool || (t.hitType != TRACE_HitWall) || !t.hitLine || (t.hitLine.Index() != switches[selnum].Index()))
					{
						// Center of wall not in line of sight of the player
						bool setfalse = tbool;
						if (!tbool)
						{
							// Maybe the line isn't a wall! Check if it can for SPAC_UseBack condition
							int plside = Level.ActorOnLineSide(pmo, switches[selnum]);
							if (plside == 1 && !(switches[selnum].activation & SPAC_UseBack)) { setfalse = true; }
							if (plside == 0 && ((switches[selnum].activation & (SPAC_Use | SPAC_UseThrough | SPAC_UseBack)) == SPAC_UseBack))
							{ setfalse = true; }
						}
						if (setfalse)
						{
							// Something is obstucting it from the player
							pmo.A_StartSound("noselect", CHAN_ITEM);
							isosel[e.Player].SetBool(false);
						}
					}
					if (isosel[e.Player].GetBool()) {
						selx[e.Player].SetFloat((switches[selnum].v1.p.x + switches[selnum].v2.p.x)*0.5);
						sely[e.Player].SetFloat((switches[selnum].v1.p.y + switches[selnum].v2.p.y)*0.5);
						selz[e.Player].SetFloat(pmo.pos.Z + viewh);
					}
				} else if (inobjarray && (selectables.Size() > selnum)) {
					[delta, target_angle, target_pitch] = lookAt(pmo.pos + (0, 0, viewh),
																 selectables[selnum].pos + (0, 0, selectables[selnum].height*0.5));
					bool tbool = players[e.Player].mo.LineTrace(target_angle, players[e.Player].mo.UseRange, target_pitch,
																TRF_ALLACTORS | TRF_BLOCKUSE, viewh, data:t);
					if (!tbool || (t.hitType != TRACE_HitActor) || !t.hitActor || (t.hitActor != selectables[selnum]))
					{
						pmo.A_StartSound("noselect", CHAN_ITEM);
						isosel[e.Player].SetBool(false);
					} else {
						selx[e.Player].SetFloat(selectables[selnum].Pos.X);
						sely[e.Player].SetFloat(selectables[selnum].Pos.Y);
						selz[e.Player].SetFloat(selectables[selnum].Pos.Z + selectables[selnum].height*0.5);
						console.printf("%s selected\n", selectables[selnum].GetClassName());
					}
				}

				if (!(inobjarray || inswarray || cursec_usable))//(selectables.Size() == 0) && (switches.Size() == 0))
				{				
					pmo.A_StartSound("noselect", CHAN_ITEM);
					isosel[e.Player].SetBool(false);
				}
			} else {
				selectables.Clear(); selobjsize = 0;
				switches.Clear(); selswsize = 0;
				selnum = 0;
				inobjarray = true;
				inswarray = false;
				cursec_usable = (pmo.CurSector.SecActTarget != null);
				BlockThingsIterator it = BlockThingsIterator.Create(pmo, players[e.Player].mo.UseRange);
				Actor obj;
				while (it.Next())
				{
					obj = it.Thing;
					if (!obj || obj.player || obj.bISMONSTER || obj.bCORPSE || obj.bMASTERNOSEE || obj.bONLYVISIBLEINMIRRORS || obj.bDORMANT || obj.bINVISIBLE || obj.bMISSILE || obj.bNOINTERACTION || obj.bICECORPSE || obj.bCRASHED || obj.bINCONVERSATION || pmo.Distance3D(obj) > players[e.Player].mo.UseRange)
						continue;
					[delta, target_angle, target_pitch] = lookAt(pmo.pos + (0, 0, viewh),
																 obj.pos + (0, 0, obj.height*0.5));
					bool tbool = pmo.LineTrace(target_angle, players[e.Player].mo.UseRange, target_pitch,
											   TRF_ALLACTORS | TRF_BLOCKUSE, viewh, data:t);
					if (!tbool || (t.hitType != TRACE_HitActor) || !t.HitActor || (t.HitActor != obj))
						continue;
					selectables.Push(obj);
					selobjsize++;
					// console.printf("Added %s\n", obj.GetClassName());
				}
				BlockLinesIterator it2 = BlockLinesIterator.Create(pmo, players[e.Player].mo.UseRange);
				Line ln;
				while (it2.Next())
				{
					ln = it2.CurLine;
					
					if (shortest_dxy(pmo, ln) > players[e.Player].mo.UseRange)
						continue;

					if (((pmo.pos.xy - ln.v1.p) dot (ln.v2.p - ln.v1.p) < 0)
						|| ((pmo.pos.xy - ln.v2.p) dot (ln.v1.p - ln.v2.p) < 0))
					{
						if ((pmo.pos.xy - ln.v1.p).length() > players[e.Player].mo.UseRange
							&& (pmo.pos.xy - ln.v2.p).length() > players[e.Player].mo.UseRange) continue;
					}

					if (!(ln.activation & (SPAC_Use | SPAC_UseThrough | SPAC_UseBack)) && (ln.special == 0))
					{
						int pside = Level.ActorOnLineSide(pmo, ln);
						Sector sec = pside == 0 ? ln.frontsector : ln.backsector;
						if (((sec != null) && (sec.SecActTarget == null)))
							continue;
					}
					switches.Push(ln);
					selswsize++;
				}
				for (int iter = 0; iter < switches.Size(); iter++)
				{
					// Something in LineTrace() prevents this from being called inside the BlockLineIterator while loop
					ln = switches[iter];
					Vector3 midln = ((ln.v1.p.x + ln.v2.p.x)*0.5,
									 (ln.v1.p.y + ln.v2.p.y)*0.5,
									 pmo.pos.Z + viewh);
					[delta, target_angle, target_pitch] = lookAt(pmo.pos + (0, 0, viewh), midln);
					bool tempbool = pmo.LineTrace(target_angle,
												  (pmo.pos.xy - midln.xy).length() + 8.0,
												  0, TRF_THRUACTORS | TRF_BLOCKUSE, viewh, data:t);
					if (!tempbool || (t.HitType != TRACE_HitWall) || !t.HitLine || (t.HitLine.Index() != ln.Index()))
					{
						// Center of wall not in line of sight of the player
						if (!tempbool)
						{
							// Maybe the line isn't a wall! Check if it can for SPAC_UseBack condition
							int plside = Level.ActorOnLineSide(pmo, ln);
							if (plside == 1 && !(ln.activation & SPAC_UseBack)) { switches.Delete(iter--); }
							if (plside == 0 && ((ln.activation & (SPAC_Use | SPAC_UseThrough | SPAC_UseBack)) == SPAC_UseBack))
							{ switches.Delete(iter--); }
						} else {
							// Something is obstucting it from the player
							switches.Delete(iter--);
						}
					}
				}
				selswsize = switches.Size();
				isosel[e.Player].SetBool(cursec_usable || (selswsize > 0) || (selobjsize > 0));
				if (cursec_usable) {
					selx[e.Player].SetFloat(pmo.pos.X);
					sely[e.Player].SetFloat(pmo.pos.Y);
					selz[e.Player].SetFloat(pmo.floorz);
				} else if (selswsize > 0) {
					inswarray = true;
					selx[e.Player].SetFloat((switches[0].v1.p.x + switches[0].v2.p.x)*0.5);
					sely[e.Player].SetFloat((switches[0].v1.p.y + switches[0].v2.p.y)*0.5);
					selz[e.Player].SetFloat(pmo.pos.Z + viewh);
					// console.printf("%d Lines selected\n", selswsize);
				} else if (selobjsize > 0) {
					inobjarray = true;
					selx[e.Player].SetFloat(selectables[0].Pos.X);
					sely[e.Player].SetFloat(selectables[0].Pos.Y);
					selz[e.Player].SetFloat(selectables[0].Pos.Z + selectables[0].height*0.5);
					console.printf("%s selected\n", selectables[0].GetClassName());
				} else {
					pmo.A_StartSound("noselect", CHAN_ITEM);
				}
			}
		}
	}
    else if (e.Name == "MYKEYUP")
    {
		int key1, key2;
		[key1, key2] = Bindings.GetKeysForCommand("rotate_cam_left");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
			rotlefts[e.Player].SetBool(false);
		else
		{
			[key1, key2] = Bindings.GetKeysForCommand("rotate_cam_right");
			if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
				rotrights[e.Player].SetBool(false);
		}

		[key1, key2] = Bindings.GetKeysForCommand("look_down");
		if ((key1 && key1 == e.Args[0]) || (key2 && key2 == e.Args[0]))
		{
			lookingdown[e.Player].SetBool(false);
		}
	}
  }

  override void WorldThingDestroyed(WorldEvent e) 
  {
	  if (e.Thing && (selobjsize > 0))
	  {
		  int thindex = selectables.Find(e.Thing);
		  if (thindex != selobjsize) // selectables.Size()
		  {
			  selectables.Delete(thindex);
			  selobjsize--;
		  }
	  }
  }

  override void WorldTick()
  {
	  for (int i = 0; i < MAXPLAYERS; i++)
	  {
		  if (PlayerInGame[i] && players[i].mo && isosel[i].GetBool())
		  {
			  if ((players[i].mo.Vel.X != 0) || (players[i].mo.Vel.Y != 0))
			  {
				  isosel[i].SetBool(false);
			  }
		  }
	  }
  }
}

class StartingInventoryHandler : EventHandler
{
	override void PlayerEntered(PlayerEvent e)
	{
		players[e.PlayerNumber].mo.A_GiveInventory("Z_NashMove", 1);
		players[e.PlayerNumber].mo.A_GiveInventory("MoveTorch", 1);
		players[e.PlayerNumber].mo.A_GiveInventory("IsometricMovement", 1);
	}
}

//Camera actor for player's scope
class ScopeCamera : Actor
{
	playerpawn ppawn;
	default
	{
	  height 0;
	  radius 0;
	  +noclip
	  +nointeraction
	  +noblockmap
	}

	override void postbeginplay()
	{
	  super.postbeginplay();
	  ppawn=playerpawn(target);
	}

	override void tick()
	{
	  if(!ppawn)
	  {
	    destroy();
	    return;
	  }

	  A_SetAngle(ppawn.angle, SPF_INTERPOLATE);
	  A_SetPitch(ppawn.pitch, SPF_INTERPOLATE);
	  A_SetRoll(ppawn.roll);

	  SetOrigin(ppawn.pos + (ppawn.radius * Cos(ppawn.angle),
				 ppawn.radius * Sin(ppawn.angle),
				 0.25 * ppawn.height), true);
	}

	States
	{
	  Spawn:
	    TNT1 A -1 NoDelay;
	    Stop;
	}
}

class CameraSpawnHandler : EventHandler // Can't be static because camera actor gets spawned
{
	cvar isodists[MAXPLAYERS], isoyaws[MAXPLAYERS], isopitches[MAXPLAYERS];
	cvar isospeeds[MAXPLAYERS], rotatelefts[MAXPLAYERS], rotaterights[MAXPLAYERS];
  override void PlayerSpawned(playerEvent e)
  {
    // Add a scope camera to each player, and store it in player.mo.tracer
    players[e.PlayerNumber].mo.tracer = Actor.Spawn("ScopeCamera", players[e.PlayerNumber].mo.pos, ALLOW_REPLACE);
    players[e.PlayerNumber].mo.tracer.target = players[e.PlayerNumber].mo;
    // When the player spawns, spawn an isometric spectator camera:
    players[e.PlayerNumber].camera = SpectatorCamera(Actor.Spawn("SpectatorCamera", players[e.PlayerNumber].mo.pos));
    players[e.PlayerNumber].camera.tracer = players[e.PlayerNumber].mo; // This makes camera follow the player
	players[e.PlayerNumber].camera.player = players[e.PlayerNumber]; // Necessary for pain and pickup screen flashes. Adds viewbob too.
    players[e.PlayerNumber].mo.tracer.tracer = players[e.PlayerNumber].camera; // Storage for retrieval in case player.camera gets changed
    int camflags = VPSF_ABSOLUTEOFFSET | VPSF_ALLOWOUTOFBOUNDS | VPSF_ORTHOGRAPHIC;
    CVar isodist = CVar.GetCVar("IsometricDist", players[e.PlayerNumber]);
    CVar isoyaw = CVar.GetCVar("IsometricYaw", players[e.PlayerNumber]);
    CVar isopitch = CVar.GetCVar("IsometricPitch", players[e.PlayerNumber]);
    SpectatorCamera(players[e.PlayerNumber].camera).Init(isodist.GetFloat(), isoyaw.GetFloat(), isopitch.GetFloat(), camflags);
    // The following has to come after Init
    // SpectatorCamera(players[e.PlayerNumber].camera).lagdistance = 150.0; // Use for a lazy camera that only follows if you get away from center
    // SpectatorCamera(players[e.PlayerNumber].camera).chasemode = 2; // 0: chase until tracer centered, 1: same but only when tracer is moving, 2: stop chase if tracer within lagdistance
  }

  override void PlayerDisconnected (PlayerEvent e)
  {
    // This is needed if a player disconnects from a multiplayer session
    if(players[e.PlayerNumber].camera) players[e.PlayerNumber].camera.destroy();
  }

  override void WorldLoaded (WorldEvent e)
  {
    for (int i = 0; i < MAXPLAYERS; i++)
      {
	if (PlayerInGame[i] && players[i].mo)
	  {
	    // Add a scope camera to each player, and store it in player.mo.tracer
	    players[i].mo.tracer = Actor.Spawn("ScopeCamera", players[i].mo.pos, ALLOW_REPLACE);
	    players[i].mo.tracer.target = players[i].mo;
	    // When World loads, spawn an isometric spectator camera:
	    players[i].camera = SpectatorCamera(Actor.Spawn("SpectatorCamera", players[i].mo.pos));
	    players[i].camera.tracer = players[i].mo; // This makes camera follow the player
		players[i].camera.player = players[i]; // Necessary for pain and pickup screen flashes. Adds viewbob too.
	    players[i].mo.tracer.tracer = players[i].camera; // Storage for retrieval in case player.camera gets changed
	    int camflags = VPSF_ABSOLUTEOFFSET | VPSF_ALLOWOUTOFBOUNDS | VPSF_ORTHOGRAPHIC;
	    isodists[i] = CVar.GetCVar("IsometricDist", players[i]);
	    isoyaws[i] = CVar.GetCVar("IsometricYaw", players[i]);
	    isopitches[i] = CVar.GetCVar("IsometricPitch", players[i]);
		isospeeds[i] = CVar.GetCVar("IsometricSpeed", players[i]);
		rotatelefts[i] = CVar.GetCVar("RotatingLeft", players[i]);
	    rotaterights[i] = CVar.GetCVar("RotatingRight", players[i]);
	    SpectatorCamera(players[i].camera).Init(isodists[i].GetFloat(), isoyaws[i].GetFloat(), isopitches[i].GetFloat(), camflags);
	    // The following has to come after Init
	    // SpectatorCamera(players[i].camera).lagdistance = 150.0; // Use for a lazy camera that only follows if you get away from center
	    // SpectatorCamera(players[i].camera).chasemode = 2; // 0: chase until tracer centered, 1: same but only when tracer is moving, 2: stop chase if tracer within lagdistance
	  }
      }
  }

  override void WorldTick()
  {
    for (int i = 0; i < MAXPLAYERS; i++)
    {
		if (PlayerInGame[i] && players[i].mo)
		{
			if ((!players[i].camera || (players[i].camera == players[i].mo)) && players[i].mo.tracer && players[i].mo.tracer.tracer)
			{
				// player.camera was reset for some reason
				// Retreive from storage
				players[i].camera = players[i].mo.tracer.tracer;
			}
			if (players[i].camera)
			{
				if (rotatelefts[i].GetBool() || rotaterights[i].GetBool())
				{
					isoyaws[i].SetFloat(isoyaws[i].GetFloat() + (rotaterights[i].GetBool() ? 1 : -1) * isospeeds[i].GetFloat());
					int camflags = VPSF_ABSOLUTEOFFSET | VPSF_ALLOWOUTOFBOUNDS | VPSF_ORTHOGRAPHIC;
					SpectatorCamera(players[i].camera).Init(isodists[i].GetFloat(),
															isoyaws[i].GetFloat(),
															isopitches[i].GetFloat(), camflags);
				}
			}
		}
	}
  }
}

class RenderCursorsOverlay : EventHandler
{
	cvar isoyaw, isopitch, isoaimdist, isoaimassist;
	cvar showwep, showscope, showscopehealth;
	cvar wepsize, wepxpos, wepypos;
	cvar scopesize, scopexpos, scopeypos;
	cvar aimbeam, scopetype, cursdist;
	cvar isosel, selx, sely, selz;
	cvar lookingdown;
	FLineTraceData t[MAXPLAYERS];
	Vector3 hitloc;
	Actor actorhit[MAXPLAYERS];

	// getActorMaxHealth(), makeHpBar(), and makeRepeating() functions
	// are derived from similar functions defined in the Target-Spy mod
	// by Author m8f: https://github.com/mmaulwurff/target-spy
	// The Target-Spy mod as released under the GPLv3 license, and a
	// copy of said license is included in the root directory as
	// GPLv3.txt

	ui static int getActorMaxHealth(Actor a)
	{
	  if (a == NULL) { return 0; }

	  if (a.player && a.player.mo) { return a.player.mo.getMaxHealth(); }

	  int maxHealth = a.spawnHealth();

	  return maxHealth;
	}

	ui static string makeHpBar(int health, int maxHealth, bool logScale = false)
	{	  
	  bool greenRedTheme = true;
	  int greenColor = 3;
	  int redColor = 6;
	  string pip = "|";
	  string emptyPip = "-";
	  double lengthMultiplier = 1.0;
    
	  int length;
	  if (logScale)               { length = int(Log(maxHealth)) * 2; }
	  else if (maxHealth >= 2000) { length = 30; }
	  else if (maxHealth >= 500)  { length = 20; }
	  else                        { length = 10; }

	  length = int(round(length * lengthMultiplier));

	  int nPips;
	  if (maxHealth < 1)
	  {
	    nPips = 0;
	  }
	  else
	  {
	    nPips = int(round(double(health) * length / maxHealth));
	    nPips = clamp(nPips, 0, length);
	  }

	  string pipColor;
	  string emptyColor;
	  if (greenRedTheme)
	  {
	    pipColor   = String.Format("\c%c", 97 + greenColor); // 'a'
	    emptyColor = String.Format("\c%c", 97 + redColor);   // 'a'
	  }
	  else
	  {
	    pipColor   = "";
	    emptyColor = "";
	  }

	  return String.Format( "%s%s%s%s"
				, pipColor
				, MakeRepeating(pip, nPips)
				, emptyColor
				, MakeRepeating(emptyPip, length - nPips)
				);

	}

	ui static string makeRepeating(string s, int n)
	{
	  if (n == 0) { return ""; }
	  string format = string.format("%%%ds", n);
	  string result = string.format(format, " ");
	  result.replace(" ", s);
	  return result;
	}
	
	override void OnRegister()
	{
	  isoyaw = CVar.GetCVar("IsometricYaw", players[consoleplayer]);
	  isopitch = CVar.GetCVar("IsometricPitch", players[consoleplayer]);
	  isoaimdist = CVar.GetCVar("IsoAimAssistMaxDist", players[consoleplayer]);
	  isoaimassist = CVar.GetCVar("IsoAimAssist", players[consoleplayer]);
	  showwep = CVar.GetCVar("IsoHUDShowWeapon", players[consoleplayer]);
	  showscope = CVar.GetCVar("IsoHUDShowTarget", players[consoleplayer]);
	  showscopehealth = CVar.GetCVar("IsoHUDShowTargetHealth", players[consoleplayer]);
	  wepsize = CVar.GetCVar("IsoHUDWeaponSize", players[consoleplayer]);
	  wepxpos = CVar.GetCVar("IsoHUDWeaponXPos", players[consoleplayer]);
	  wepypos = CVar.GetCVar("IsoHUDWeaponYPos", players[consoleplayer]);
	  scopesize = CVar.GetCVar("IsoHUDTargetSize", players[consoleplayer]);
	  scopexpos = CVar.GetCVar("IsoHUDTargetXPos", players[consoleplayer]);
	  scopeypos = CVar.GetCVar("IsoHUDTargetYPos", players[consoleplayer]);
	  aimbeam  = CVar.GetCVar("IsoAimBeam", players[consoleplayer]);
	  scopetype = CVar.GetCVar("IsoHUDScopeType", players[consoleplayer]);
	  cursdist = CVar.GetCVar("IsoCursorDist", players[consoleplayer]);
	  isosel = CVar.GetCVar("IsoSelected", players[consoleplayer]);
	  selx = CVar.GetCVar("SelXPos", players[consoleplayer]);
	  sely = CVar.GetCVar("SelYPos", players[consoleplayer]);
	  selz = CVar.GetCVar("SelZPos", players[consoleplayer]);
	  lookingdown = CVar.GetCVar("IsoLookingDown", players[consoleplayer]);
	  hitloc = (0, 0, 0);
	  for (int i = 0; i < MAXPLAYERS; i++)
	  {
	    actorhit[i] = NULL; // false;
	  }
	}

	override void WorldTick()
	{
	  for (int i = 0; i < MAXPLAYERS; i++)
	  {
		if (PlayerInGame[i] && players[i].mo)
		{
			PlayerPawn pmo = players[i].mo;
			float max_dist = isoaimdist.GetFloat();
			if (pmo.LineTrace(pmo.angle, max_dist, pmo.pitch, TRF_NOSKY, pmo.viewheight*pmo.player.crouchfactor, data:t[i]))
			{
				if ((t[i].HitType == TRACE_HitActor) && t[i].hitActor.bISMONSTER && !t[i].hitActor.bFRIENDLY && !t[i].hitActor.bCORPSE)
					actorhit[i] = t[i].HitActor; // true;
				else
					actorhit[i] = NULL; // false;
			}
			else
				actorhit[i] = NULL; // false;
		}
	  }
	}
	
	override void RenderOverlay(RenderEvent e)
	{
	  if (!automapactive && (gamestate == GS_LEVEL) && (players[consoleplayer].camera.player == players[consoleplayer]))
	  {
	    PlayerPawn pmo = players[consoleplayer].mo;
	    float pang = pmo.angle;
	    float cyaw = isoyaw.GetFloat();
	    float cpit = isopitch.GetFloat();
	    float max_dist = isoaimdist.GetFloat();
	    
	    StatusBar.BeginHUD();
		Vector2 scaleFactor = StatusBar.GetHUDScale();

	    if(showwep.GetBool())
	    {
		  int wsize = (int)(clamp(wepsize.GetInt(), 1, 100)*2/scaleFactor.x);
	      int wxpos = (int)(200*clamp(wepxpos.GetFloat(), -1.0, 1.0)*scaleFactor.x);
	      int wypos = (int)(-200*clamp(wepypos.GetFloat(), 0.0, 1.0)*scaleFactor.y);
		  let curwep = players[consoleplayer].readyweapon;
		  let curpsp = players[consoleplayer].FindPSprite(PSP_WEAPON);
		  if (players[consoleplayer].mo.health > 0) // necessary check to prevent crash
		  {
			  StatusBar.DrawInventoryIcon(curwep, (wxpos, wypos),
										  StatusBar.DI_SCREEN_CENTER_BOTTOM | StatusBar.DI_ITEM_CENTER,
										  (curpsp.CurState == curwep.FindState("Fire") ) ||
										  curpsp.CurState.InStateSequence(curwep.FindState("Ready")) ? 1.0 : 0.7, // alpha
										  (2*wsize, wsize), (0.03*wsize, 0.03*wsize)); // boxsize, scale
		  }
	    }

		bool ldown = lookingdown.GetBool();
	    int cursordist = ldown ? players[consoleplayer].mo.viewheight*2.0 : cursdist.GetInt(); // 150;
	    float diffangle = pmo.deltaangle(pang, cyaw);
	    int cosine = (int)(-0.5*cursordist*Cos(diffangle));
	    int sine = (int)(cursordist*Sin(diffangle));
		int selcos = 0;
		int selsin = 0;
	    if(players[consoleplayer].camera && players[consoleplayer].camera.tracer)
	    {
	      if(players[consoleplayer].camera.ViewPos && (players[consoleplayer].camera.ViewPos.Flags & VPSF_ORTHOGRAPHIC))
	      { // If using orthographic projection
			  Vector3 playerdisp = players[consoleplayer].camera.tracer.pos - players[consoleplayer].camera.pos;
			  diffangle = pmo.deltaangle(playerdisp.Angle(), cyaw);
			  // Translate game/map (x, y, z) position into screen (x, y) location
			  double xproj = 640/players[consoleplayer].camera.ViewPos.offset.length();
			  double yproj = Sin(cpit)*xproj;
			  double zproj = Cos(cpit)*xproj;
			  cosine -= (int)(playerdisp.xy.length()*Cos(diffangle)*yproj); // + playerdisp.z*zproj); no need since Spectatorcamera tracks zpos
			  if (!ldown) cosine -= (int)(players[consoleplayer].camera.tracer.height*0.5*zproj);
			  sine += (int)(playerdisp.xy.length()*Sin(diffangle)*xproj);
			  // else use libEye to locate camera.tracer's screen coordinates

			  // Do the same for position of selected object or switch
			  Vector3 seldisp = (selx.GetFloat(), sely.GetFloat(), selz.GetFloat()) - players[consoleplayer].camera.pos;
			  diffangle = pmo.deltaangle(seldisp.Angle(), cyaw);
			  selcos -= (int)(seldisp.xy.length()*Cos(diffangle)*yproj + seldisp.z*zproj);
			  selsin += (int)(seldisp.xy.length()*Sin(diffangle)*xproj);
	      }
	    }

	    if (isoaimassist.GetBool() && !ldown)
	    {
	      StatusBar.DrawImage("sprites/aim_hair.png", (sine/scaleFactor.x, cosine/scaleFactor.y),
				  StatusBar.DI_SCREEN_CENTER | StatusBar.DI_ITEM_CENTER, scale: (2.4/scaleFactor.x, 2.4/scaleFactor.y));
	    } else {
	      StatusBar.DrawImage("sprites/dir_hair.png", (sine/scaleFactor.x, cosine/scaleFactor.y),
				  StatusBar.DI_SCREEN_CENTER | StatusBar.DI_ITEM_CENTER, scale: (1.5/scaleFactor.x, 1.5/scaleFactor.y));
	    }

		if (isosel.GetBool())
	    {
	      StatusBar.DrawImage("sprites/sel_hair.png", (selsin/scaleFactor.x, selcos/scaleFactor.y),
				  StatusBar.DI_SCREEN_CENTER | StatusBar.DI_ITEM_CENTER, scale: (1.5/scaleFactor.x, 1.5/scaleFactor.y));
	    }

	    if (actorhit[consoleplayer])
	    {
	      int cosine2 = 0;
	      int sine2 = 0;
	      if(players[consoleplayer].camera && players[consoleplayer].camera.ViewPos && (players[consoleplayer].camera.ViewPos.Flags & VPSF_ORTHOGRAPHIC))
	      { // If using orthographic projection
			  Vector3 tardisp = actorhit[consoleplayer].pos + (0., 0., actorhit[consoleplayer].height/2)
				  - players[consoleplayer].camera.pos;
			  diffangle = pmo.deltaangle(tardisp.Angle(), cyaw);
			  // Translate game/map (x, y, z) position into screen (x, y) location
			  double xprj = 720/players[consoleplayer].camera.ViewPos.offset.length();
			  double yprj = Sin(cpit)*xprj;
			  double zprj = Cos(cpit)*xprj;
			  cosine2 -= (int)(tardisp.xy.length()*Cos(diffangle)*yprj);
			  cosine2 -= (int)(tardisp.z*zprj);
			  sine2 += (int)(tardisp.xy.length()*Sin(diffangle)*xprj);
	      } // else use libEye to locate camera.tracer's screen coordinates
	      cosine -= (int)(-0.25*cursordist*Cos(diffangle));
	      sine -= (int)(0.5*cursordist*Sin(diffangle));	      
	      int halfw = (int)(Screen.GetWidth()/2);
	      int halfh = (int)(Screen.GetHeight()/2);
	      Screen.DrawLine(sine+halfw, cosine+halfh, sine2+halfw, cosine2+halfh, Color(aimbeam.GetString()));
	      StatusBar.DrawImage("sprites/tar_hair.png", (sine2/scaleFactor.x, cosine2/scaleFactor.y),
				  StatusBar.DI_SCREEN_CENTER | StatusBar.DI_ITEM_CENTER, scale: (1.5/scaleFactor.x, 1.5/scaleFactor.x));

	      if(showscope.GetBool())
	      {
		double tardist = (actorhit[consoleplayer].pos - players[consoleplayer].camera.pos).length();
		tardist -= (actorhit[consoleplayer].radius + pmo.radius);
		double degree = 57.2957795 * actorhit[consoleplayer].height / (tardist > 0.0 ? tardist: 1.0); // in degrees
		texman.setcameratotexture(players[consoleplayer].mo.tracer, "CAMTEX_1", degree);

		float ssize = 0.04*clamp(scopesize.GetInt(), 1, 100)/scaleFactor.x;
		int sxpos = (int)(200*clamp(scopexpos.GetFloat(), -1.0, 1.0)*scaleFactor.x);
		int sypos = (int)(-200*clamp(scopeypos.GetFloat(), 0.0, 1.0)*scaleFactor.y);
		StatusBar.DrawImage("CAMTEX_1", (sxpos, sypos),
				    StatusBar.DI_SCREEN_CENTER_BOTTOM | StatusBar.DI_ITEM_CENTER,
				    scale: (ssize, ssize));
		string scopesprite = (scopetype.GetInt() == 1 ? "sprites/scope_sight.png" : "sprites/scope_sight2.png");
		if (scopetype.GetInt() == 3) scopesprite = "sprites/scope_sight3.png";
		StatusBar.DrawImage(scopesprite, (sxpos, sypos),
				    StatusBar.DI_SCREEN_CENTER_BOTTOM | StatusBar.DI_ITEM_CENTER,
				    scale: (0.8*ssize, 0.8*ssize));

		if(showscopehealth.GetBool())
		{
		  string hpBar = makeHpBar(actorhit[consoleplayer].health,
					   getActorMaxHealth(actorhit[consoleplayer]));
		  HUDFont monospaceSmallfont = HUDFont.Create(smallfont, smallfont.GetCharWidth("0"), true);
		  StatusBar.DrawString(monospaceSmallfont, hpBar, (sxpos-(int)(30*ssize), sypos-(int)(40*ssize)),
				       StatusBar.DI_SCREEN_CENTER_BOTTOM | StatusBar.DI_ITEM_CENTER,
				       scale: (0.8*ssize, 0.8*ssize));
		}
	      }
	    }
	  }
	}
}

class IsoAimAssistHandler : StaticEventHandler
{
	// copying RicardoLuis0's Aim Assist Mod v0.8
	// https://forum.zdoom.org/viewtopic.php?t=62895
	FLineTraceData tarray[MAXPLAYERS];
	cvar aimassists[MAXPLAYERS]; // They say that GetCVar() inside WorldTick() is slow?
	cvar max_dists[MAXPLAYERS];
	cvar lookingdown[MAXPLAYERS];

	override void WorldLoaded (WorldEvent e)
	{
		for (int i = 0; i < MAXPLAYERS; i++)
		{
			if (PlayerInGame[i] && players[i].mo)
			{
				aimassists[i] = CVar.GetCVar("IsoAimAssist", players[i]);
				max_dists[i] = CVar.GetCVar("IsoAimAssistMaxDist", players[i]);
				lookingdown[i] = CVar.GetCVar("IsoLookingDown", players[i]);
				players[i].mo.bPickup = CVar.GetCVar("IsoAllowTouchPickup", players[i]).GetBool(); // Didn't want another staticeventhandler
				CVar.GetCVar("IsoSelected", players[i]).SetBool(false); // Didn't want another staticeventhandler
			}
		}
	}

	// do linetrace and get results
	play Actor,double,Vector3 doTrace(PlayerPawn a,double i_angle,double i_rotation,Actor closest,double closest_distance, double max_dist){
		FLineTraceData t;
		Vector3 hitloc=(0,0,0);
		//do a linetrace around i_a and i_r in a circle
		if(a.LineTrace(a.angle+(sin(i_rotation)*i_angle),			//trace angle
							max_dist,							//trace max distance
							a.pitch+(cos(i_rotation)*i_angle*8),		//trace pitch (modified by factor 8)
							TRF_NOSKY,								//trace flags
							a.viewheight*a.player.crouchfactor,		//trace height
							data:t									//output struct
		)){
			if(t.hitType==TRACE_HitActor){//if hit is an actor
				
				if(t.hitActor.bISMONSTER&&!t.hitActor.bFRIENDLY&&!t.hitActor.bCORPSE){//if hit is a monster and not friendly
					if(!closest||a.Distance3D(t.HitActor)>closest_distance){//if it's closer than last hit
						//change this as new closest
						closest=t.HitActor;
						closest_distance=a.Distance3D(t.HitActor);
						hitloc=t.HitLocation;
					}
				}
			}
		}
		return closest,closest_distance,hitloc;
	}

	//get angle and delta from two positions
	static vector3, double, double lookAt(Vector3 pos1, Vector3 pos2){
		//calculate difference between pos1 and pos2 (level.Vec3Diff takes portals into account)
		
		Vector3 delta=level.Vec3Diff(pos1, pos2);
		
		//calculate angle and pitch to other actor
		double target_angle = atan2(delta.y, delta.x);
		double target_pitch = -asin(delta.z/delta.length());

		return delta, target_angle, target_pitch;
	}

	//main method, does all work
	bool doAim(int pnum)
	{
		PlayerPawn pawn = players[pnum].mo;
		Actor closest = null;
		Actor hit = null;
		Vector3 hitloc = (0, 0, 0);
		cvar aimassist = aimassists[pnum];
		float max_dist = max_dists[pnum].GetFloat();
		float closest_distance = max_dist + 1;
		
		//check straight ahead
		[closest, closest_distance, hitloc] = doTrace(pawn, 0, 0, closest, closest_distance, max_dist);

		// Convert these to CVars?
		double precision = 0.5;
		double radial_precision = 8;
		double max_angle = 8;

		//check in a circle around the direction player's looking
		for(double i_a = 0; i_a <= max_angle; i_a += precision) {
			for(double i_r = 0; i_r <= 360 && !closest; i_r += radial_precision) {
			  [closest, closest_distance, hitloc] = doTrace(pawn, i_a, i_r, closest, closest_distance, max_dist);
			}
		}

		//if there was an enemy found
		if(closest){
		  float pheight = pawn.viewheight*pawn.player.crouchfactor;
		  Vector3 aimheight=(0, 0, closest.height);
		  Vector3 delta;
		  double target_angle,target_pitch;
		  Vector3 view=pawn.pos + (0, 0, pheight);
		  //get target angle and pitch
		  [delta, target_angle, target_pitch] = lookAt(view, closest.pos + aimheight);
			
		  //check if view is obstructed
		  FLineTraceData t;
		  pawn.LineTrace(target_angle, max_dist, target_pitch,
				 TRF_NOSKY, pawn.viewheight*pawn.player.crouchfactor, data:t);
		  //aim correction
		  if((t.hitType != TRACE_HitActor) || (t.hitActor != closest)){
		    //try to aim at correct z
		    [delta, target_angle, target_pitch] = lookAt(view, (hitloc.x, hitloc.y, closest.pos.z + aimheight.z));
		    pawn.LineTrace(target_angle, max_dist, target_pitch, TRF_NOSKY, pawn.viewheight * pawn.player.crouchfactor, data:t);
		  }
		  if((t.hitType != TRACE_HitActor) || (t.hitActor != closest)){
		    //try to aim at correct xy
		    [delta, target_angle, target_pitch] = lookAt(view, (closest.pos.x, closest.pos.y, hitloc.z));
		    pawn.LineTrace(target_angle, max_dist, target_pitch, TRF_NOSKY, pawn.viewheight * pawn.player.crouchfactor, data:t);
		  }
		  /* if((t.hitType != TRACE_HitActor) || (t.hitActor != closest)){ */
		  /*   // target closest */
		  /*   [delta, target_angle, target_pitch] = lookAt(view, hitloc); */
		  /* } */
		  if((t.hitType != TRACE_HitActor) || (t.hitActor != closest)){
		    // Stop aiming
		    return false;
		  }

		  //get angle difference
		  double angle_diff = pawn.DeltaAngle(pawn.angle, target_angle);
		  double pitch_diff = pawn.DeltaAngle(pawn.pitch, target_pitch);

		  double rot_speed = 1;
		  if (aimassist.GetBool())
		  {
		    //check rotation speed
		    if(abs(angle_diff) > rot_speed){
		      //if rotation speed is lower than difference, add/subtract rotation speed
		      pawn.A_SetAngle(pawn.angle + (angle_diff > 0 ? rot_speed : -rot_speed), SPF_INTERPOLATE);
		    } else {
		      //if rotation speed is higher than differece, set to target angle
		      pawn.A_SetAngle(target_angle, SPF_INTERPOLATE);
		    }
		  }
		  
		  if(abs(pitch_diff) > rot_speed){
		    //if rotation speed is lower than difference, add/subtract rotation speed
		    pawn.A_SetPitch(pawn.pitch + (pitch_diff > 0 ? rot_speed : -rot_speed), SPF_INTERPOLATE);
		  } else {
		    //if rotation speed is higher than differece, set to target pitch
		    pawn.A_SetPitch(target_pitch, SPF_INTERPOLATE);
		  }
		  return true;
		} else {
		  return false;
		}
	}

	override void WorldTick()
	{
	  for (int i = 0; i < MAXPLAYERS; i++)
	  {
	    if (PlayerInGame[i] && players[i].mo)
	    {
			if (lookingdown[i].GetBool()) players[i].mo.A_SetPitch(45);
			else if (!doAim(i)) players[i].mo.A_SetPitch(0);
	    }
	  }
	}

}

// Taken from a demo meant for the D4D team.
// I can't track down who to credit here.
// Is it MajorCooke?
// Slightly modified to take camera angle into account.
Class D4DHandler : EventHandler
{
	override void OnRegister()
	{
		SetOrder(666);
		Super.OnRegister();
	}
	
	override void WorldLoaded(WorldEvent e)
	{
		ArrowManager = D4VisualDamageManager.Create();
	}
	
	override void RenderOverlay(RenderEvent e)
	{
		RenderDamageIndicators(e);
	}
	
	override void WorldTick()
	{
		TickDamageIndicators();
	}
	
	override void WorldThingDamaged(WorldEvent e)
	{
		Actor damaged =	e.Thing;
		
		if (damaged.player)
		{
			Actor	src = e.DamageSource,
					inf = e.Inflictor;
			
			if (!src && !inf)
				return;
			
			AddEventDamageIndicator(e);
		}
	
	}
	
	
	
	private D4VisualDamageManager ArrowManager;
	private ui void RenderDamageIndicators(RenderEvent e)
	{
		if (ArrowManager)
			ArrowManager.Render(e);
	}
	
	private void TickDamageIndicators()
	{
		if (ArrowManager)
			ArrowManager.Tick();
	}
	
	private void AddEventDamageIndicator(WorldEvent e)
	{
		if (ArrowManager)
			ArrowManager.AddIndicator(e.DamageSource, e.Inflictor, e.Thing, e.Damage);
	}
	
	void AddDamageIndicator(Actor src, Actor inf, Actor plr, int damage)
	{
		if (ArrowManager)
		{
			ArrowManager.AddIndicator(src, inf, plr, damage);
		}
	}
}

//==============================================================================
// Damage Indicators
//==============================================================================

Class D4VisualDamageManager play
{
	private Array<D4DamageIndicator> Arrows;
	private int Timer;
	const ClearTimer = 5 * 5;
	
	static D4VisualDamageManager Create()
	{
		let vdm = new('D4VisualDamageManager');
		vdm.Init();
		return vdm;
	}
	
	protected void Init()
	{
		Arrows.Clear();
	}
	
	void AddIndicator(Actor src, Actor inf, Actor plr, int damage = 0)
	{
		if ((!src && !inf) || !plr || src == plr)
			return;
		
		for (int i = 0; i < Arrows.Size(); i++)
		{
			if (Arrows[i] && Arrows[i].src == src)
			{
				Arrows[i].ResetTimer();
			//	Console.Printf("Updated arrow.");
				return;
			}
		}
		
		let arrow = new('D4DamageIndicator');
		arrow.src = src;
		arrow.inf = inf;
		if (src)	arrow.srcpos = src.pos;
		else if ((inf && inf.bMISSILE && inf.target) || inf)
		{
			src = (inf.target) ? inf.target : inf;
			arrow.srcpos = src.pos;
		}
		if (inf)	arrow.infpos = inf.pos;
		arrow.plr = plr;
		arrow.tex = TexMan.CheckForTexture(D4DamageIndicator.texname,TexMan.Type_Any);
		arrow.ResetTimer();
		arrow.Init();
		Arrows.Push(arrow);
	}
	
	void Tick()
	{
		int size = Arrows.Size();
		if (size < 1)	return;
		
		// Tick all the arrows and keep their information up to date.
		for (int i = 0; i < size; i++)
		{
			if (Arrows[i])
				Arrows[i].Tick();
		}
		
		// Memory management. Remove all empty slots.
		if (++Timer >= ClearTimer)
		{
			Timer = 0;
			Array<D4DamageIndicator> temp; temp.Clear();
			
			for (int i = 0; i < size; i++)
				if (Arrows[i])	temp.Push(Arrows[i]);
			
			Arrows.Move(temp);
		}
	}
	
	ui void Render(RenderEvent e)
	{
		if (Arrows.Size() < 1)
			return;
		
		PlayerInfo plr = players[consoleplayer];
		let CDIAlpha = Cvar.GetCvar('D4D_DIAlpha', plr);
		let CDIScale = Cvar.GetCvar('D4D_DIScale', plr);
		double Alpha = 1.0, Scale = 0.5;
		
		if (CDIAlpha)	Alpha = CDIAlpha.GetFloat();
		if (CDIScale)	Scale = CDIScale.GetFloat();
		
		int size = Arrows.Size();
		for (int i = 0; i < size; i++)
		{
			let arrow = D4DamageIndicator(Arrows[i]);
			if (arrow && arrow.pinfo == plr)
				Arrows[i].Render(e, Alpha, Scale);
		}
	}
}

Class D4DamageIndicator play
{
	const texname = "sprites/DmgDir2.png";
	Actor inf, src, plr;
	PlayerInfo pinfo;
	Vector3 infpos, srcpos;
	TextureID tex;
	private double Alpha, Scale;
	private int Timer;
	
	private bool hadsrc, hadinf;
	private Vector2 siz;
	private Shape2D flat;
	private Shape2DTransform trans;
	
	void Init()
	{
		hadsrc = src != null;
		hadinf = inf != null;
		pinfo = plr.player;
		flat = new("Shape2D");
		
		// simple coords
		flat.PushCoord((0,0));	// 0
		flat.PushCoord((1,0));	// 1
		flat.PushCoord((0,1));	// 2
		flat.PushCoord((1,1));	// 3
		
		// PushTriangle takes INDEXES of coords pushed to it in order from
		// first to last, as numbered above.
		flat.PushTriangle(0,2,1); // (0,0) (0,1) (1,0)
		flat.PushTriangle(2,3,1); // (0,1) (1,1) (1,0)
		siz = TexMan.GetScaledSize(tex);
		
		// Create the vertices and push them into the array.
		Vector2 vertices[4];
		vertices[0] = (-siz.x,-siz.y);
		vertices[1] = ( siz.x,-siz.y);
		vertices[2] = (-siz.x, siz.y);
		vertices[3] = ( siz.x, siz.y);
		
		flat.Clear(Shape2D.C_Verts);
		for ( int i=0; i<4; i++ ) flat.PushVertex(vertices[i]);
		
		trans = new('Shape2DTransform');
	}
	
	void ResetTimer(int time = -1)
	{
		if (time < 1)	time = (5 * 5);
		Timer = time;
	}
	
	void Tick()
	{
		if (--Timer < 0 || !plr)
		{
		//	Console.Printf("Destroyed");
			Destroy();	return;	
		}
		
		if (!src && inf && inf.bMISSILE)
			src = inf.target;
		
		if (src)	srcpos = src.pos;
		if (inf)	infpos = inf.pos;
		
	}
	const ThirtyFifth = (1.0 / 35.0);
	ui void Render( RenderEvent e, double _Alpha, double _Scale)
	{	
		// Alpha is already clamped below.
		double Alpha = (ThirtyFifth * Timer) * _Alpha;
		double Scale = _Scale;
		
		if (bDESTROYED || Alpha <= 0.0 || !plr || plr.pos == srcpos || !hadsrc)
			return;
		
		// Grab the player preferences.
		trans.Clear();
		
		// Rotate the damage indicator towards the one responsible.
		Vector3 diff = level.Vec3Diff(srcpos, plr.pos);
		double ang = VectorAngle(diff.X, diff.Y);
		if (pinfo.camera) ang = -plr.DeltaAngle(pinfo.camera.angle, ang);
		else ang = -plr.DeltaAngle(plr.angle, ang);

		Vector2 s = (Screen.GetWidth() / 2, Screen.GetHeight() / 2);
		
		double off = (siz.y + (siz.y * Scale)) * 0.75;
		Vector2 add = (-sin(ang) * off, cos(ang) * off);
		s += add;
		
		trans.Scale((1, 1) * Scale);
		trans.Rotate(ang + 180.0);
		trans.Translate(s);
		
		flat.SetTransform(trans);
		
			
		// draw the shape
		Screen.DrawShape(tex,false,flat,DTA_Alpha, Clamp(Alpha, 0.0, 1.0));
		
	}
}
